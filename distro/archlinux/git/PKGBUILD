# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Matthias "matthiaskrgr" Kr√ºger
# Maintainer: Jookia <166291@gmail.com>
pkgname=desurium-git
pkgver=20130218
pkgrel=1
pkgdesc="A community-driven digital distribution service for gamers (open source and development version)."
url="http://github.com/lodle/Desurium"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('cmake' 'git' 'boost' 'scons' 'gendesk' 'sed')
depends=('v8' 'tinyxml' 'boost-libs' 'flashplugin' 'libxxf86vm' 'gperf'
         'curl' 'c-ares' 'libnotify' 'libgnome-keyring' 'yasm' 'libxslt'
         'cups' 'flex' 'alsa-lib' 'libjpeg-turbo' 'speex' 'libevent'
         'gconf' 'xdg-user-dirs' 'flac' 'libwebp' 'xdg-utils')
provides=('desura')
conflicts=('desura' 'desurium' 'desurium-cef')

_gitroot=git://github.com/lodle/Desurium.git
_gitname=Desurium


if [ "${CARCH}" = "x86_64" ]; then
    depends+=('binutils-multilib')
  else  # 32 bit
    depends+=('binutils')
fi

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname"
    msg "Removing build files..."
    # if this fails, git clean -dfx
    rm build -rf
    msg "Updating..."
    git pull --no-tags
    msg "The local files are updated."
  else
    msg "Cloning..."
    git clone "$_gitroot" "$_gitname" --depth 1
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  cd "$srcdir/$_gitname"
  if [ ! -d build ] ; then
    mkdir build
  fi
  cd build
  msg "Running cmake..."

  if [ "${CARCH}" = "x86_64" ]; then
    CMAKE_ARGS="
    -D32BIT_SUPPORT=1
    -DCMAKE_INSTALL_PREFIX=/usr
    -DBINDIR=/usr/bin
    -DDATADIR=/usr/share/
    -DRUNTIME_LIBDIR=/usr/lib/
    -DBUILD_CEF=ON"
  else  # 32 bit
    CMAKE_ARGS="
    -DCMAKE_INSTALL_PREFIX=/usr
    -DBINDIR=/usr/bin
    -DDATADIR=/usr/share/
    -DRUNTIME_LIBDIR=/usr/lib/
    -DBUILD_CEF=ON"
  fi
  cmake $CMAKE_ARGS ..
  msg "Running make..."
  make
}

check() {
  cd "$srcdir/Desurium/build"
  make test
}

package() {
  cd "$srcdir/$_gitname/build"

  msg "Compiling into pkgdir this time..."
  make DESTDIR="$pkgdir/" install

  cd $srcdir

  gendesk -n ../PKGBUILD
  sed -i \
    -e 's/Icon=.*$/Icon=desura/' \
    -e 's/Exec=.*$/Exec=desura/' \
    -e 's/Categories=.*$/Categories=Game;/' \
    -e 's/Name=.*$/Name=Desurium/' ${pkgname}.desktop

  install -m644 -D \
    ${srcdir}/${_gitname}/src/branding_desurium/data/desura_lin.png \
    ${pkgdir}/usr/share/pixmaps/desura.png

  install -m644 -D \
    ${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/desura.desktop

  msg "Compiling done"
  
  mkdir -p ${pkgdir}/usr/bin
}

# vim:set ts=2 sw=2 et:
