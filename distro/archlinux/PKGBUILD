# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Matthias "matthiaskrgr" Kr√ºger
# Maintainer: Jookia <166291@gmail.com>
pkgname=desurium-git
pkgver=20120430
pkgrel=1
pkgdesc="A community-driven digital distribution service for gamers (open source version)"
url="http://github.com/lodle/Desurium"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('cmake' 'git' 'boost' 'subversion' 'scons' 'gawk' 'gendesk' 'sed')
depends=('v8' 'tinyxml' 'sqlite' 'boost-libs' 'bzip2' 'gtk2' 'openssl'
         'curl' 'c-ares' 'libnotify' 'nss' 'libgnome-keyring' 'bison'
         'cups' 'flex' 'alsa-lib' 'libjpeg-turbo' 'libxpm' 'libx11'
         'gconf' 'xdg-user-dirs' 'flac' 'libpng' 'speex' 'zlib'
         'xdg-utils' 'libwebp' 'libevent' 'libxslt' 'yasm')
provides=('desura')
conflicts=('desura')

_gitroot=git://github.com/lodle/Desurium.git
_gitname=Desurium

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname"
    msg "Removing build files..."
    # if this fails, git clean -dfx
    ./build_make_clean.sh || echo "./build_make_clean.sh failed, running  git clean -dfx ..." ; git clean -dfx
    msg "Updating..."
    git pull origin --depth 1 --no-tags
    msg "The local files are updated."
  else
    msg "Cloning..."
    git clone "$_gitroot" "$_gitname" --depth 1 --single-branch --branch master
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  MULTILIB=0
  installed="$(pacman -Qsq binutils-multilib)"

  if [ "$installed" != "" ]; then
    MULTILIB=1
  fi

  cd "$srcdir/$_gitname"
  if [ ! -d build ] ; then
    mkdir build
  fi
  cd build
  msg "Running cmake..."
  cmake -D32BIT_SUPPORT=$MULTILIB -DCMAKE_INSTALL_PREFIX=/usr/share/desurium ..
  msg "Running make..."
  make
}

package() {
  cd "$srcdir/$_gitname/build"

  msg "Compiling into pkgdir this time..."
  make DESTDIR="$pkgdir/" install

  cd $srcdir

  gendesk -n ../PKGBUILD
  sed -i \
    -e 's/Icon=.*$/Icon=desurium/' \
    -e 's/Exec=.*$/Exec=desurium/' \
    -e 's/Categories=.*$/Categories=Game;/' \
    -e 's/Name=.*$/Name=Desurium/' ${pkgname}.desktop

  install -m644 -D \
    ${srcdir}/${_gitname}/src/branding_desurium/data/desura_lin.png \
    ${pkgdir}/usr/share/pixmaps/desurium.png

  install -m644 -D \
    ${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/desurium.desktop

  msg "Compiling done"
  
  mkdir -p ${pkgdir}/usr/bin
  ln -sf /usr/share/desurium/run.sh ${pkgdir}/usr/bin/desurium
}

# vim:set ts=2 sw=2 et:
