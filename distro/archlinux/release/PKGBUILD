# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Matthias "matthiaskrgr" Kr√ºger
# Maintainer: Jookia <166291@gmail.com>
pkgname=desurium-git
pkgver=20121022
pkgrel=1
pkgdesc="A community-driven digital distribution service for gamers (open source version)"
url="http://github.com/lodle/Desurium"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('cmake' 'git' 'boost' 'subversion' 'scons' 'gendesk' 'sed')
depends=('v8' 'tinyxml' 'sqlite' 'boost-libs' 'bzip2' 'gtk2' 'openssl'
         'curl' 'c-ares' 'libnotify' 'nss' 'libgnome-keyring' 'bison'
         'cups' 'flex' 'alsa-lib' 'libjpeg-turbo' 'libxpm' 'libx11'
         'gconf' 'xdg-user-dirs' 'flac' 'libpng' 'speex' 'zlib' 'gperf'
         'xdg-utils' 'libwebp' 'libevent' 'libxslt' 'yasm' 'libxxf86vm')
provides=('desura')
conflicts=('desura')

_gitroot=git://github.com/lodle/Desurium.git
_gitname=Desurium

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname"
    msg "Removing build files..."
    # if this fails, git clean -dfx
    rm build -rf
    msg "Updating..."
    git pull --no-tags
    msg "The local files are updated."
  else
    msg "Cloning..."
    git clone "$_gitroot" "$_gitname" --depth 1
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  MULTILIB=0

  if pacman -Qsq binutils-multilib ; then
    MULTILIB=1
  fi

  cd "$srcdir/$_gitname"
  if [ ! -d build ] ; then
    mkdir build
  fi
  cd build
  msg "Running cmake..."
  CMAKE_ARGS="
    -D32BIT_SUPPORT=$MULTILIB
    -DCMAKE_INSTALL_PREFIX=/usr
    -DBINDIR=/usr/bin
    -DDATADIR=/usr/share/
    -DRUNTIME_LIBDIR=/usr/lib/
    -DBUILD_CEF=ON"
  cmake $CMAKE_ARGS ..
  msg "Running make..."
  make
}

check() {
  cd "$srcdir/Desurium/build"
  make test
}

package() {
  cd "$srcdir/$_gitname/build"

  msg "Compiling into pkgdir this time..."
  make DESTDIR="$pkgdir/" install

  cd $srcdir

  gendesk -n ../PKGBUILD
  sed -i \
    -e 's/Icon=.*$/Icon=desura/' \
    -e 's/Exec=.*$/Exec=desura/' \
    -e 's/Categories=.*$/Categories=Game;/' \
    -e 's/Name=.*$/Name=Desurium/' ${pkgname}.desktop

  install -m644 -D \
    ${srcdir}/${_gitname}/src/branding_desurium/data/desura_lin.png \
    ${pkgdir}/usr/share/pixmaps/desura.png

  install -m644 -D \
    ${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/desura.desktop

  msg "Compiling done"
  
  mkdir -p ${pkgdir}/usr/bin
}

# vim:set ts=2 sw=2 et:
